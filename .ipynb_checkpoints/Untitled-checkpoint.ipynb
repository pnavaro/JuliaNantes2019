{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "create_animation (generic function with 1 method)"
      ]
     },
     "execution_count": 1,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "# -*- coding: utf-8 -*-\n",
    "# # Deep Water problem\n",
    "#\n",
    "# CPU version\n",
    "\n",
    "using ProgressMeter, FFTW, LinearAlgebra, Plots\n",
    "\n",
    "abstract type AbstractModel end\n",
    "abstract type InitialData end\n",
    "\n",
    "# +\n",
    "struct Mesh\n",
    "\n",
    "    N    :: Int64\n",
    "    x    :: Vector{Float64}\n",
    "    kmin :: Float64\n",
    "    kmax :: Float64\n",
    "    k    :: Vector{Float64}\n",
    "\n",
    "    function Mesh(param :: NamedTuple)\n",
    "\n",
    "        xmin = - Float64(param.L)\n",
    "        xmax =   Float64(param.L)\n",
    "        N    =   param.N\n",
    "        \n",
    "        dx   = (xmax-xmin)/N\n",
    "        x    = zeros(Float64, N)\n",
    "        x   .= range(xmin, stop=xmax, length=N+1)[1:end-1] \n",
    "        dk   = 2π/(N*dx)\n",
    "        kmin = -N/2*dk\n",
    "        kmax = (N/2-1)*dk\n",
    "        k    = zeros(Float64, N)\n",
    "        k   .= dk .* vcat(0:N÷2-1, -N÷2:-1)\n",
    "\n",
    "        new( N, x, kmin, kmax, k)\n",
    "\n",
    "    end\n",
    "\n",
    "end\n",
    "\n",
    "# +\n",
    "struct Times\n",
    "\n",
    "    Nt   :: Int\n",
    "    dt   :: Float64\n",
    "\n",
    "    function Times( dt , tfin)\n",
    "        Nt = length(0:dt:tfin)\n",
    "        new( Nt, dt)\n",
    "    end\n",
    "\n",
    "end\n",
    "\n",
    "\n",
    "# +\n",
    "\"\"\"\n",
    "    RK4(params)\n",
    "\n",
    "Runge-Kutta fourth order solver.\n",
    "\n",
    "\"\"\"\n",
    "mutable struct RK4\n",
    "    \n",
    "    Uhat :: Array{ComplexF64,2}\n",
    "    dU   :: Array{ComplexF64,2}\n",
    "\n",
    "    function RK4( param::NamedTuple, model::AbstractModel )\n",
    "\n",
    "        n = param.N\n",
    "\n",
    "        Uhat = zeros(ComplexF64, (n,2))\n",
    "        dU   = zeros(ComplexF64, (n,2))\n",
    "\n",
    "        new( Uhat, dU)\n",
    "\n",
    "    end\n",
    "\n",
    "end\n",
    "\n",
    "# +\n",
    "function step!(s  :: RK4,\n",
    "               f! :: AbstractModel,\n",
    "               U  :: Array{ComplexF64,2},\n",
    "               dt :: Float64)\n",
    "\n",
    "    \n",
    "    Threads.@threads for i in eachindex(U)\n",
    "        @inbounds s.Uhat[i] = U[i]\n",
    "    end\n",
    "\n",
    "    f!( s.Uhat )\n",
    "\n",
    "    Threads.@threads for i in eachindex(U)\n",
    "        @inbounds s.dU[i]   = s.Uhat[i]\n",
    "        @inbounds s.Uhat[i] = U[i] + dt/2 * s.Uhat[i]\n",
    "    end\n",
    "\n",
    "    f!( s.Uhat )\n",
    "\n",
    "    Threads.@threads for i in eachindex(U)\n",
    "        @inbounds s.dU[i]   += 2 * s.Uhat[i]\n",
    "        @inbounds s.Uhat[i]  = U[i] + dt/2 * s.Uhat[i]\n",
    "    end\n",
    "\n",
    "    f!( s.Uhat )\n",
    "\n",
    "    Threads.@threads for i in eachindex(U)\n",
    "        @inbounds s.dU[i]   += 2 * s.Uhat[i]\n",
    "        @inbounds s.Uhat[i]  = U[i] + dt * s.Uhat[i]\n",
    "    end\n",
    "\n",
    "    f!( s.Uhat )\n",
    "\n",
    "    Threads.@threads for i in eachindex(U)\n",
    "        @inbounds s.dU[i] += s.Uhat[i]\n",
    "        @inbounds U[i]    += dt/6 * s.dU[i]\n",
    "    end\n",
    "\n",
    "end\n",
    "\n",
    "# +\n",
    "struct BellCurve <: InitialData\n",
    "\n",
    "    h :: Vector{ComplexF64}\n",
    "    u :: Vector{ComplexF64}\n",
    "\n",
    "    function BellCurve(p :: NamedTuple,theta :: Real)\n",
    "\n",
    "        mesh  = Mesh(p)\n",
    "        h     = zeros(ComplexF64, mesh.N)\n",
    "        h    .= exp.(.-((abs.(mesh.x)).^theta).*log(2))\n",
    "        u     = zeros(ComplexF64, mesh.N)\n",
    "\n",
    "        new( h, u )\n",
    "\n",
    "    end\n",
    "\n",
    "end\n",
    "# -\n",
    "\n",
    "\"\"\"\n",
    "    Matsuno(params)\n",
    "\n",
    "\"\"\"\n",
    "mutable struct Matsuno <: AbstractModel\n",
    "\n",
    "    label    :: String\n",
    "    datasize :: Int\n",
    "    Γ        :: Array{Float64,1}\n",
    "    Dx       :: Array{ComplexF64,1}\n",
    "    H        :: Array{ComplexF64,1}\n",
    "    Π⅔       :: BitArray{1}\n",
    "    ϵ        :: Float64\n",
    "    hnew     :: Vector{ComplexF64}\n",
    "    unew     :: Vector{ComplexF64}\n",
    "    I₀       :: Vector{ComplexF64}\n",
    "    I₁       :: Vector{ComplexF64}\n",
    "    I₂       :: Vector{ComplexF64}\n",
    "    I₃       :: Vector{ComplexF64}\n",
    "\n",
    "    Px       :: FFTW.FFTWPlan\n",
    "\n",
    "    function Matsuno(param::NamedTuple)\n",
    "\n",
    "        label    = \"Matsuno\"\n",
    "        datasize = 2\n",
    "        ϵ        = param.ϵ\n",
    "        mesh     = Mesh(param)\n",
    "        Γ        = abs.(mesh.k)\n",
    "        Dx       =  1im * mesh.k        # Differentiation\n",
    "        H        = -1im * sign.(mesh.k) # Hilbert transform\n",
    "        Π⅔       = Γ .< mesh.kmax * 2/3 # Dealiasing low-pass filter\n",
    "\n",
    "        hnew = zeros(ComplexF64, mesh.N)\n",
    "        unew = zeros(ComplexF64, mesh.N)\n",
    "\n",
    "        I₀ = zeros(ComplexF64, mesh.N)\n",
    "        I₁ = zeros(ComplexF64, mesh.N)\n",
    "        I₂ = zeros(ComplexF64, mesh.N)\n",
    "        I₃ = zeros(ComplexF64, mesh.N)\n",
    "\n",
    "        Px  = plan_fft(hnew; flags = FFTW.MEASURE)\n",
    "\n",
    "        new(label, datasize, Γ, Dx, H, Π⅔, ϵ,\n",
    "            hnew, unew, I₀, I₁, I₂, I₃, Px)\n",
    "    end\n",
    "end\n",
    "\n",
    "\n",
    "# +\n",
    "function (m::Matsuno)( U :: Array{ComplexF64,2})\n",
    "\n",
    "\n",
    "    Threads.@threads for i in eachindex(m.hnew)\n",
    "        @inbounds m.hnew[i] = m.Γ[i] * U[i,1]\n",
    "    end\n",
    "\n",
    "    ldiv!(m.unew, m.Px, m.hnew )\n",
    "\n",
    "    Threads.@threads for i in eachindex(m.hnew)\n",
    "        @inbounds m.hnew[i] = m.Dx[i] * U[i,1]\n",
    "    end\n",
    "\n",
    "    ldiv!(m.I₁, m.Px, m.hnew)\n",
    "\n",
    "    Threads.@threads for i in eachindex(m.unew)\n",
    "        @inbounds m.unew[i] *= m.I₁[i]\n",
    "    end\n",
    "\n",
    "    mul!(m.I₁, m.Px, m.unew)\n",
    "\n",
    "    Threads.@threads for i in eachindex(m.hnew)\n",
    "        @inbounds m.I₁[i] = m.I₁[i] * m.ϵ * m.Π⅔[i] - m.hnew[i]\n",
    "    end\n",
    "\n",
    "    ldiv!(m.hnew, m.Px, view(U,:,1))\n",
    "    ldiv!(m.unew, m.Px, view(U,:,2))\n",
    "\n",
    "    Threads.@threads for i in eachindex(m.hnew)\n",
    "        @inbounds m.I₂[i] = m.hnew[i] * m.unew[i]\n",
    "    end\n",
    "\n",
    "    mul!(m.I₃, m.Px, m.I₂)\n",
    "\n",
    "    Threads.@threads for i in eachindex(m.H)\n",
    "        @inbounds U[i,1]  = m.H[i] * U[i,2]\n",
    "        @inbounds m.I₀[i] = m.Γ[i] * U[i,2]\n",
    "    end\n",
    "\n",
    "    ldiv!(m.I₂, m.Px, m.I₀)\n",
    "\n",
    "    Threads.@threads for i in eachindex(m.hnew)\n",
    "        @inbounds m.I₂[i] *= m.hnew[i]\n",
    "    end\n",
    "\n",
    "    mul!(m.hnew, m.Px, m.I₂)\n",
    "\n",
    "    Threads.@threads for i in eachindex(m.unew)\n",
    "        @inbounds U[i,1] -= (m.I₃[i] * m.Dx[i] + m.hnew[i] * m.H[i]) * m.ϵ * m.Π⅔[i]\n",
    "        @inbounds m.I₃[i]  = m.unew[i]^2\n",
    "    end \n",
    "\n",
    "    mul!(m.unew, m.Px, m.I₃)\n",
    "\n",
    "    Threads.@threads for i in eachindex(m.unew)\n",
    "        @inbounds U[i,2]  =  m.I₁[i] - m.unew[i] * m.Dx[i] * m.ϵ/2 * m.Π⅔[i]\n",
    "    end \n",
    "\n",
    "end\n",
    "\n",
    "# +\n",
    "function create_animation( mesh, times, model, data )\n",
    "\n",
    "    prog = Progress(times.Nt,1)\n",
    "\n",
    "    hr = zeros(Float64,mesh.N)\n",
    "    ur = zeros(Float64,mesh.N)\n",
    "\n",
    "    anim = @animate for l in 1:size(data)[end]\n",
    "\n",
    "        pl = plot(layout=(2,1))\n",
    "\n",
    "        hr .= real(ifft(view(data,:,1,l)))\n",
    "        ur .= real(ifft(view(data,:,2,l)))\n",
    "\n",
    "        plot!(pl[1,1], mesh.x, hr;\n",
    "              ylims=(-0.6,1),\n",
    "              title=\"physical space\",\n",
    "              label=model.label)\n",
    "\n",
    "        plot!(pl[2,1], fftshift(mesh.k),\n",
    "              log10.(1e-18.+abs.(fftshift(fft(hr))));\n",
    "              ylims=(-20,5),\n",
    "              title=\"frequency\",\n",
    "              label=model.label)\n",
    "\n",
    "        next!(prog)\n",
    "\n",
    "    end when mod(l, 100) == 0\n",
    "\n",
    "    gif(anim, \"anim_\"*model.label*\".gif\", fps=15)\n",
    "\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "4"
      ]
     },
     "execution_count": 2,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "Threads.nthreads()"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "metadata": {},
   "outputs": [
    {
     "name": "stderr",
     "output_type": "stream",
     "text": [
      "\u001b[32mProgress: 100%|█████████████████████████████████████████| Time: 0:00:20\u001b[39m\n",
      "\u001b[32mProgress: 100%|█████████████████████████████████████████| Time: 0:00:23\u001b[39m\n",
      "┌ Info: Saved animation to \n",
      "│   fn = /Users/navaro/JuliaProjects/FourierAdvections.jl/notebooks/anim_Matsuno.gif\n",
      "└ @ Plots /Users/navaro/.juliapro/JuliaPro_v1.1.1.1/packages/Plots/oiirH/src/animation.jl:90\n"
     ]
    },
    {
     "data": {
      "text/html": [
       "<img src=\"anim_Matsuno.gif\" />"
      ],
      "text/plain": [
       "Plots.AnimatedGif(\"/Users/navaro/JuliaProjects/FourierAdvections.jl/notebooks/anim_Matsuno.gif\")"
      ]
     },
     "execution_count": 6,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "# +\n",
    "function main_cpu( N :: Int64 ; animation = true )\n",
    "\n",
    "    param = ( ϵ  = 1/2,\n",
    "              N  = N,\n",
    "              L  = 10.,\n",
    "              T  = 5.,\n",
    "              dt = 0.001 )\n",
    "    \n",
    "    mesh    = Mesh(param)\n",
    "    times   = Times(param.dt, param.T)\n",
    "    init    = BellCurve(param,2.5)\n",
    "    model   = Matsuno(param)\n",
    "    solver  = RK4(param,model)\n",
    "    \n",
    "    U       = zeros(ComplexF64,(mesh.N,2))\n",
    "    U[:,1] .= model.Π⅔ .* fft(init.h) \n",
    "    U[:,2] .= model.Π⅔ .* fft(init.u)\n",
    "\n",
    "    dt = times.dt\n",
    "    data = zeros(ComplexF64,(mesh.N,2,times.Nt))\n",
    "\n",
    "    @showprogress 1 for j in 1:times.Nt\n",
    "        step!(solver, model, U, dt)\n",
    "        data[:,:,j] .= U\n",
    "    end\n",
    "\n",
    "    if animation\n",
    "        create_animation( mesh, times, model, data )\n",
    "    end\n",
    "\n",
    "end\n",
    "# -\n",
    "main_cpu( 2^14 ; animation = true )"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "@webio": {
   "lastCommId": null,
   "lastKernelId": null
  },
  "kernelspec": {
   "display_name": "Julia 1.1.1",
   "language": "julia",
   "name": "julia-1.1"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.1.1"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
